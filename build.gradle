apply plugin: 'war'
apply plugin: 'appengine'

project.webAppDirName = 'war'

def appEmail  = "umasankar.usp@gmail.com"		

def appengineVersion = "1.9.19"

sourceSets {
    main {
        java { srcDir 'src/java' }
    }
    test {  
      java { srcDir 'src/javatests'}
    } 
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "com.google.appengine:gradle-appengine-plugin:1.9.19"
    }
}

repositories {
    mavenCentral()
}

dependencies {
	appengineSdk "com.google.appengine:appengine-java-sdk:${appengineVersion}"
	compile "javax.servlet:servlet-api:2.5"
	compile "com.google.appengine:appengine-api-1.0-sdk:${appengineVersion}"
}

appengine {

    httpPort = 8888
    downloadSdk = true
    
    jvmFlags = ['-Dappengine.fullscan.seconds=5', '-Xdebug', '-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=8000']
  
    appcfg {
    
        email = "${appEmail}"
		noCookies = true
		oauth2 = true
    }
}


// Reload tasks

import com.google.appengine.AppEnginePlugin

task reloadClasses (type: Copy) {
  from sourceSets.main.output
  into "${AppEnginePlugin.getExplodedAppDirectory(project)}/WEB-INF/classes"

  dependsOn classes
}

task reloadWebAppDir (type: Copy) {
  from (project.convention.getPlugin(WarPluginConvention).webAppDir) {
    exclude "WEB-INF/*"
  }
  into AppEnginePlugin.getExplodedAppDirectory(project)
}

task customReload {
  dependsOn reloadClasses, reloadWebAppDir
}
